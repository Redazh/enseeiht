#  GLPK model file created by SUN for RO teaching


###############################  Model ###############################



###############################  Sets  ###############################

set DEMANDES;

set FLUIDES;

set MAGAZINS;



################### Variables ###################

## To declare binary variables : 
var Q{i in MAGAZINS ,j in FLUIDES ,k in DEMANDES}, >=0; 

var Livraison{i in DEMANDES, j in MAGAZINS}, binary;


###################  Constants: Data to load   #########################

param MatriceFluidesDemandes{i in DEMANDES ,j in FLUIDES};
param MatriceStocks{i in MAGAZINS ,j in FLUIDES};
param MatriceDeCout{i in MAGAZINS ,j in FLUIDES};
param MatriceCoutFixe{i in DEMANDES, j in MAGAZINS};
param MatriceCoutVariable{i in DEMANDES, j in MAGAZINS};

#Determination du BigM
param maxStock := max{i in MAGAZINS ,j in FLUIDES} MatriceStocks[i,j];
param BigM := card(FLUIDES) * maxStock;


################### Constraints ###################


s.t. RespectCommande{j in FLUIDES , k in DEMANDES}:
  sum{i in MAGAZINS} Q[i,j,k] = MatriceFluidesDemandes[k,j];

s.t. RespectStocks{i in MAGAZINS ,j in FLUIDES}:
  sum{k in DEMANDES} Q[i,j,k] <= MatriceStocks[i,j];

s.t. ContrainteBinaire{i in DEMANDES, j in MAGAZINS}:
  sum{k in FLUIDES} Q[j, k, i] <= Livraison[i, j] * BigM;

###### Objective ######

minimize Cout: 
		(sum{i in MAGAZINS}  sum{j in FLUIDES} sum{k in DEMANDES} Q[i,j,k] * MatriceDeCout[i,j]) + (sum{i in DEMANDES} sum{j in MAGAZINS} Livraison[i,j] * (MatriceCoutVariable[i,j] + MatriceCoutFixe[i,j])); 

#end;


#default data

data;

set DEMANDES :=
D1
D2;

set FLUIDES :=
F1
F2;

set MAGAZINS := 
M1
M2
M3;


param MatriceFluidesDemandes : F1 F2 :=
D1 2 0
D2 1 3;

param MatriceStocks : F1 F2 :=
M1 2.5 1
M2 1 2
M3 2 1;

param MatriceDeCout : F1 F2 :=
M1 1 1
M2 2 3
M3 3 2;

param MatriceCoutFixe : M1 M2 M3 :=
D1 110 90 100
D2 110 90 100;

param MatriceCoutVariable : M1 M2 M3 :=
D1 10 1 5
D2 2 20 10;




end;
